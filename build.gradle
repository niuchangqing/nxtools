subprojects {
	apply plugin: 'java-library'
	apply plugin: "maven-publish"
	apply plugin: "signing"

	group 'cn.nxtools'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
	}
	dependencies {
		testImplementation group: 'junit', name: 'junit', version: "${junitVersion}"
	}


	java {
		withJavadocJar()
		withSourcesJar()
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
				pom {
					licenses {
						license {
							name = 'The Apache License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}
					developers {
						developer {
							id = 'niuchangqing'
							name = 'niuchangqing'
							email = 'ncqxwh@163.com'
						}
					}
					scm {
						connection = 'scm:git:github.com:niuchangqing/nxtools.git'
						developerConnection = 'scm:git:github.com:niuchangqing/nxtools.git'
						url = 'https://github.com/niuchangqing/nxtools'
					}
				}
			}
		}
		repositories {
			maven {
				// change URLs to point to your repos, e.g. http://my.org/repo
				def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
				def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
				url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				credentials {
					username ossrhUsername
					password ossrhPassword
				}
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}


	javadoc {
		if(JavaVersion.current().isJava9Compatible()) {
			options.addBooleanOption('html5', true)
		}
	}
}
